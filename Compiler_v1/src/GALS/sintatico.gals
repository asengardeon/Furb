#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
min: [a-z]
mai: [A-Z]
def: {mai}{min}+
dig: [0-9]

int: [1-9]
frac: (, ( {int} | 0)+ )

letra1: [^ ' \n \\]
letra2: [^ \" \\]
literal1:  '( {letra1} | \\\\ | \\' )*'
literal2: \"( {letra2} | \\\\ | \\\" )*\" 

allLinha: [^\n]
allBloco: [^ \] ]

//quetão e)
bloco: \-\- \[ {allBloco}* \]

//questão d)
linha: \-\- {allLinha}*

#Tokens
//questão a)
identificador: {min} ( {min} | {def} )* ( _{dig}+ )?

//questão b)
real: (0 | {int} ( {int} | 0 )* )+ {frac}?

//quaestão c)
literal: {literal1} | {literal2}

//ignorando
:[\s\t\n]* | ({bloco} | {linha})*

"+"
"-"
"*"
"/"
"=="
"~="
"<="
">="
"<"
">"
"="
")"
"("
";"
":"
"["
"]"
","


ADD = identificador : "add"
AND = identificador : "and"
BOOLEAN = identificador : "boolean"
CASE = identificador : "case"
CHARACTER = identificador : "character"
CONTINUE = identificador : "continue"
COUNT = identificador : "count"
DELETE = identificador : "delete"
DO = identificador : "do"
ELEMENTOF = identificador : "elementOf"
ELSE = identificador : "else"
END = identificador : "end"
ENDIF = identificador : "endif"
EXIT = identificador : "exit"
FALSE = identificador : "false"
IF = identificador : "if"
LIST = identificador : "list"
NOT = identificador : "not"
NUMBER = identificador : "number"
OR = identificador : "or"
PROGRAM = identificador : "program"
READ = identificador : "read"
SIZE = identificador : "size"
TRUE = identificador : "true"
WRITE = identificador : "write"
#NonTerminals
<program>
<decvars>
<decvar>
<decvar1>
<tipos>
<tiposimples>
<tipolista>
<listaid>
<listaid1>
<entrada>
<saida>
<listaexp>
<listaexp1>
<loop>
<final>
<selecao>
<else>
<listaaux>
<listacmd>
<listacmd1>
<comando>
<comando1> 
<expressao>
<expressao1>
<valor>
<relacional>
<relacional1>
<termo>
<termo1>
<aritmetica>
<aritmetica1>
<elemento_expressao>
<operador_relacional>
<identificadores>

#Grammar
<program> 		::= PROGRAM <decvars><listacmd>END;
<decvars> 		::= <decvar> |î;
<decvar>  		::= <tipos>":"<listaid>";"<decvar1>;
<decvar1>		::= î | <decvar>;
<tipos>   		::= <tiposimples> | <tipolista>;
<tiposimples>		::= NUMBER | CHARACTER | BOOLEAN;
<tipolista>		::= LIST"["<tiposimples>","real"]";
<listaid>		::= identificador <listaid1>;
<listaid1>		::= î | "," <listaid>;
<entrada>		::= READ"("<listaid>")" ";";
<saida>			::= WRITE"("<listaexp>")" ";";
<listaexp>		::= <expressao><listaexp1>;
<listaexp1>		::= î | ","<listaexp>;
<loop>			::= DO<listacmd>CASE"("<expressao>")"<final>";";
<final>			::= CONTINUE | EXIT;
<selecao>		::= IF"("<expressao>")"<listacmd><else>ENDIF";";
<else>			::= î | ELSE<listacmd>;
<listaaux>		::= ADD"("<expressao>","<expressao>")" | DELETE"("<expressao>")";
<listacmd>		::= <comando><listacmd1>;
<listacmd1>		::= î | <listacmd>;
<comando>		::= identificador<comando1> | <entrada> | <saida> | <loop> | <selecao>;
<comando1> 		::= <listaid1> "=" <expressao> ";" |<listaaux>";";
<expressao>		::= <valor> <expressao1>;
<expressao1>		::= î |  AND <valor> <expressao1> | OR <valor> <expressao1>; 
<valor>			::= <relacional> | TRUE | FALSE | NOT <valor>;
<relacional>		::= <aritmetica> <relacional1>;
<relacional1>		::= <operador_relacional> <aritmetica> | î;
<operador_relacional> 	::= "==" | "~=" | "<" | "<=" | ">" | ">=" ;
<aritmetica>		::= <termo><aritmetica1>;
<aritmetica1>		::= î| "+" <termo> <aritmetica1>| "-" <termo><aritmetica1>;
<termo>			::= <elemento_expressao><termo1>;
<termo1>		::= î | "*"<elemento_expressao> <termo1>	| "/" <elemento_expressao><termo1>; 
<elemento_expressao>	::= identificador<identificadores> | real | literal | "("<expressao>")" | "+"<elemento_expressao> | "-"<elemento_expressao>;
<identificadores>	::= î | COUNT | SIZE | ELEMENTOF "("<expressao>")" ;

